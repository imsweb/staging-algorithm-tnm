plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.2.4"
    id 'com.bmuschko.nexus' version '2.3.1'
    id "io.codearte.nexus-staging" version "0.21.2"  // logs into Sonotype OSS and does a "Close" and "Release"
    id 'com.adarshr.test-logger' version '2.0.0'
    id "com.github.ben-manes.versions" version "0.28.0"
}

group = 'com.imsweb'
version = '1.9.2'
description = 'TNM algorithm for the java-staging-client'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api 'com.imsweb:staging-client-java:4.7'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.squareup.retrofit2:retrofit:2.9.0'
    testImplementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.staging.tnm'
        )
    }
}

test {
    maxHeapSize = "1g"
}

testlogger {
    theme 'mocha'
}

checkstyle {
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    toolVersion = '4.0.3'
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

wrapper {
    gradleVersion = '6.4.1'
    distributionType = Wrapper.DistributionType.ALL
}

modifyPom {
    project {
        name 'TNM Staging Algorithm'
        description 'TNM algorithm for the staging-client-java'
        url 'https://github.com/imsweb/staging-algorithm-tnm'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/staging-algorithm-tnm'
            connection 'scm:https://github.com/imsweb/staging-algorithm-tnm.git'
            developerConnection 'scm:git@github.com:imsweb/staging-algorithm-tnm.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/staging-algorithm-tnm/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
        }
    }
}
